10 FOR i=4 TO 15:INK i,RND*27:NEXT
20 SYMBOL AFTER 41 
30 SYMBOL 202,0,24,44,70,143,135,110,24
40 SYMBOL 143,238,111,246,238,187,221,107,214
50 SYMBOL 206,238,111,246,238,187,221,107,214
60 SYMBOL 191,60,90,126,102,60,106,169,165
70 SYMBOL 42,28,28,72,62,9,20,34 
80 SPEED KEY 30,2
90 MODE 0:PEN 1:PAPER 0:BORDER 4:INK 0,4:INK 2,11:INK 1,2:INK 3,26
100 tabl=1:vie=3:vie$=STRING$(3,224)
110 FOR i=1 TO 100:PLOT RND*640,RND*400,RND*15::NEXT
120 PEN 1:LOCATE (20-9)/2,2:PRINT"STAR KING"
130 PEN 2:LOCATE 6,10:PRINT"1:MANETTE"
140 LOCATE 6,12:PRINT"2:CLAVIER"
150 a$=INKEY$:IF a$="1" OR a$="2" THEN 160 ELSE GOTO 150
160 IF a$="1" THEN d=75:g=74:h=72:b=73
170 IF a$="2" THEN d=62:g=71:h=17:b=28
180 X=2:Y=2:U=10:R=10:PEN 2:MODE 0
190 PAPER 3:PEN 1:FOR I=1 TO 20 :LOCATE I,1:PRINT CHR$(143):NEXT  
200 FOR I=1 TO 24 :LOCATE 1,I:PRINT CHR$(143):NEXT   
210 FOR I=1 TO 20 :LOCATE I,24:PRINT CHR$(143):NEXT   
220 FOR I=1 TO 24 :LOCATE 20,I:PRINT CHR$(143):NEXT   
221 PAPER 0
230 PEN 3:LOCATE 2,23:PRINT"NIVEAU ";:PEN 1:PRINT USING"##";tabl;:PEN 3:PRINT" VIE";:PEN 1:PRINT " ";vie$:PEN 2
240 dd=0:ON tabl GOSUB 270,670,680,690,700,720,740,760,820,860,910
250 PAPER 3:PEN 1:FOR I=2 TO 19:LOCATE I,22:PRINT CHR$(206):NEXT:PAPER 0
260 GOTO 280  
270 fin=358:lap=6:FOR I=2 TO 19:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(202);:NEXT:NEXT:RETURN
280 PEN 3:LOCATE Y,X:PRINT "*":LOCATE U,R:PRINT CHR$(191):FOR s=1 TO 1000:NEXT:f$=" "
290 dd=dd+1:IF dd=10-lap THEN GOSUB 560:dd=0  
300 IF ttt=1 THEN th=th-1 :IF th=0 THEN GOSUB 810
310 ' Commande
320 IF INKEY(d)=0 THEN 370
330 IF INKEY(g)=0 THEN 410
340 IF INKEY(h)=0 THEN 450
350 IF INKEY(b)=0 THEN 490
360 FOR hh=1 TO 20:NEXT:GOTO 290
370 LOCATE Y,X:PRINT " ":y=y+1:IF y=20 THEN y=19
380 LOCATE Y,X:CH$=COPYCHR$(#0):IF CH$=CHR$(202) THEN gain=gain+1:SOUND 1,200,1 
390 IF CH$=CHR$(143) THEN y=y-1
400 LOCATE Y,X:PRINT "*":GOTO 640
410 LOCATE Y,X:PRINT " ":y=y-1:IF y=1 THEN y=2
420 LOCATE Y,X:CH$=COPYCHR$(#0):IF CH$=CHR$(202) THEN gain=gain+1:SOUND 1,200,1  
430 IF CH$=CHR$(143) THEN y=y+1
440 LOCATE Y,X:PRINT "*":GOTO 640 
450 LOCATE Y,X:PRINT " ":x=x-1:IF x=1 THEN x=2
460 LOCATE Y,X:CH$=COPYCHR$(#0):IF CH$=CHR$(202) THEN gain=gain+1:SOUND 1,200,1
470 IF CH$=CHR$(143) THEN x=x+1
480 LOCATE Y,X:PRINT "*":GOTO 640
490 LOCATE Y,X:PRINT " ":x=x+1:IF x=22 THEN x=21 
500 LOCATE Y,X:CH$=COPYCHR$(#0):IF CH$=CHR$(202) THEN gain=gain+1:SOUND 1,200,1 
510 IF CH$=CHR$(143) THEN x=x-1
520 LOCATE Y,X:PRINT "*":GOTO 640
530 IF vie=0 THEN RUN
540 vie$=CHR$(15)+CHR$(1)+STRING$(vie,224)+CHR$(15)+CHR$(2)+STRING$(3-vie,225)
550 gain=0:GOTO 180 
560 PEN 2:LOCATE U,R:IF f$=CHR$(143) THEN PEN 3:PRINT f$:ELSE PRINT f$:PEN 3
570 IF Y>U THEN U=U+1
580 IF Y<U THEN U=U-1
590 IF X>R THEN R=R+1
600 IF X<R THEN R=R-1
610 LOCATE U,R:F$=COPYCHR$(#0):LOCATE U,R:PRINT CHR$(22);CHR$(1);CHR$(191);CHR$(22);CHR$(0)
620 IF U=Y AND X=R THEN SOUND 1,4000: vie=vie-1:GOTO 530
630 RETURN
640 IF gain=fin THEN SOUND 1,200:gain=0:TABl=TABl+1:GOTO 180
650 PEN 3:LOCATE 1,25:PRINT"PIERRES:";CHR$(15);CHR$(1);gain;CHR$(15);CHR$(3);"/";CHR$(15);CHR$(1);fin;CHR$(15);CHR$(3)
660 GOTO 290 
670 PEN 2:FOR I=2 TO 19:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(202);:NEXT:NEXT:PEN 3:FOR ss=2 TO 21:LOCATE 11,ss:PRINT CHR$(206);:NEXT:LOCATE 11,12:PRINT " ":lap=4:fin=338:RETURN  
680 PEN 2:FOR I=2 TO 19:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(202);:NEXT:NEXT:PEN 3:FOR ss=2 TO 21:LOCATE 6,ss:PRINT CHR$(206);:NEXT:LOCATE 6,12:PRINT " ":FOR ss=2 TO 21:LOCATE 14,ss:PRINT CHR$(206);:NEXT:LOCATE 14,12:PRINT " ":fin=318:RETURN
690 PEN 2:lap=6:FOR I=2 TO 19:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(202);:NEXT:NEXT:PEN 3:FOR ss=2 TO 21:LOCATE 6,ss:PRINT CHR$(206);:NEXT:LOCATE 6,12:PRINT " ":FOR ss=2 TO 21:LOCATE 14,ss:PRINT CHR$(206);:NEXT:LOCATE 14,12:PRINT " ":fin=318:RETURN
700 PEN 3:FOR I=2 TO 19:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(206);:NEXT:NEXT:PEN 2:FOR I=6 TO 14:FOR ss=8 TO 16:LOCATE I,ss:PRINT CHR$(202);:gh=gh+1:NEXT:NEXT:lap=2:fin=78
710 X=8:Y=6:U=14:R=16:PEN 2:RETURN
720 PEN 3:FOR I=2 TO 19:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(206);:NEXT:NEXT:PEN 2:FOR I=6 TO 14:FOR ss=8 TO 16:LOCATE I,ss:PRINT CHR$(202);:gh=gh+1:NEXT:NEXT:lap=6:fin=78
730 X=8:Y=6:U=14:R=16:PEN 2:RETURN
740 PEN 3:FOR I=2 TO 19:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(206);:NEXT:NEXT:PEN 2:FOR I=6 TO 14:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(202);:NEXT:NEXT:FOR I=2 TO 19:FOR ss=8 TO 16:LOCATE I,ss:PRINT CHR$(202);:NEXT:NEXT:lap=4:fin=259
750 X=8:Y=6:U=14:R=16:PEN 2:RETURN
760 PEN 3:FOR i=2 TO 4:FOR ss=2 TO 19:LOCATE ss,i:PRINT CHR$(143):NEXT:NEXT
770 FOR i=18 TO 21:FOR ss=2 TO 19:LOCATE ss,i:PRINT CHR$(143):NEXT:NEXT
780 FOR i=2 TO 18:FOR ss=2 TO 4:LOCATE ss,i:PRINT CHR$(143):NEXT:NEXT
790 FOR i=2 TO 18:FOR ss=17 TO 19:LOCATE ss,i:PRINT CHR$(143):NEXT:NEXT  
800 fin=1:ttt=1:th=100:lap=8:X=5:Y=5:U=14:R=16:PEN 2:RETURN
810 tabl=tabl+1:th=0:GOTO 180
820 PEN 2 :FOR I=2 TO 19:FOR ss=2 TO 21:LOCATE I,ss:PRINT CHR$(202);:NEXT:NEXT
830 PEN 3:FOR ss=2 TO 21 STEP 2:FOR i=2 TO 19 STEP 4:LOCATE i,ss:PRINT CHR$(143):NEXT:NEXT
840 FOR i=4 TO 19 STEP 4:FOR ss=3 TO 21 STEP 2:LOCATE i,ss:PRINT CHR$(143):NEXT:NEXT  
850 lap=4:fin=270:RETURN  
860 PEN 3:FOR i=2 TO 4:FOR ss=2 TO 19:LOCATE ss,i:PRINT CHR$(143):NEXT:NEXT
870 FOR i=18 TO 21:FOR ss=2 TO 19:LOCATE ss,i:PRINT CHR$(143):NEXT:NEXT
880 FOR i=2 TO 18:FOR ss=2 TO 4:LOCATE ss,i:PRINT CHR$(143):NEXT:NEXT
890 FOR i=2 TO 18:FOR ss=17 TO 19:LOCATE ss,i:PRINT CHR$(143):NEXT:NEXT
900 fin=5:PEN 2:LOCATE 8,12:PRINT STRING$(5,202):lap=8:X=5:Y=5:U=14:R=16:PEN 2:RETURN   
910 MODE 0:a$(1)="Bravo !":a$(2)="Vous avez gagne !"
920 FOR I=1 TO 2:LOCATE (20-LEN(a$(i)))/2,7*i+i:FOR u=1 TO LEN(a$(i))
930 m$=MID$(a$(i),u,1):d$=BIN$(ASC(m$),8)
940 FOR j=1 TO 8:f$=MID$(d$,j,1):f=VAL(f$):SOUND 1,200*f,5:NEXT:PRINT M$;:NEXT:NEXT  
950 FOR i=1 TO 1000:NEXT:RUN
960 DIM a$(30)
970 FOR i=1 TO 22:READ a$(i):NEXT
980 MODE 2
990 PEN 1:INK 1,26:BORDER 0:INK 0,0:FOR i=0 TO 12:PAPER i:CLS:NEXT
1000 LOCATE 1,25:FOR I=1 TO 22:IF i>5 AND i<20 THEN PRINT CHR$(227):ELSE PRINT "#":PRINT
1001 IF i>4 AND i<19 THEN PRINT CHR$(149):PRINT CHR$(149);
1002 FOR u=1 TO LEN(a$(i))  
1010 m$=MID$(a$(i),u,1):d$=BIN$(ASC(m$),8)
1020 IF INKEY(47)=0 THEN RUN
1021 IF m$="*" THEN m$=CHR$(24)+CHR$(24)
1030 FOR j=1 TO 8-(ASC(m$)/40)*2:f$=MID$(d$,j,1):f=VAL(f$):IF f=0 THEN SOUND 1,0,4,0 ELSE SOUND 1,28,4,7
1040 NEXT:PRINT M$;" ";:NEXT:NEXT     
1050 PRINT "#"
1060 PRINT:PRINT:PRINT "End"
1070 FOR i=1 TO 1000:IF INKEY(47)=0 THEN RUN ELSE NEXT:FOR i=0 TO 12:PAPER i:CLS:NEXT:RUN
1080 DATA "  >Demande de message numero *******6**5**7**8**3**S au megatelex ********B**6**0**9**8**4 ","","","   >*************Message accorde< "
1090 DATA "Etat Major a la base Plutoniene B6: "
1100 DATA "La ~Force Pluton~ a trouve sous le cratere C10 une grotte contenant beaucoup " 
1110 DATA "d'uraniun et appartenant a l'enemi .Agent Kirk ,vous devez penetrer dans la "
1120 DATA "grotte ,prendre les pierres et eviter l'enemi de type LUNAIRE 3 a tete "
1130 DATA "chercheuse .La grotte est composee de dix sous-sols remplis de pierres qui "
1140 DATA "devront toutes etre prises .D'apres les donnees des espions ,chaque etage "
1150 DATA "est surveille par un robot LUNAIRE 3 qui survole les pieces . "
1160 DATA "Vous serez equipe du systeme antiradiation WALK 6 a tres faible inertie "
1170 DATA "mais sans arme:les radiations etant trop fortes ,les tires vous detruiraient "
1180 DATA "Attention: ce systeme ne peut etre touche que trois fois sinon il explose . "
1190 DATA "Vous aurez la possibilite d'utiliser le clavier de l'ordinateur central pour "
1200 DATA "vous deplacer (Z C [ ;) ou alors le manche . "  
1210 DATA ""
1220 DATA "Bonne chance... "
1230 DATA "","","","  >Fin du message numero *********6**5**7**8**3**S<"
